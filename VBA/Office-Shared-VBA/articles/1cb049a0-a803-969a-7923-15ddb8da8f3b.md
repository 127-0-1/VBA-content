
# Sync Object (Office)

The  **Sync** property of the **Document** object in Microsoft Word, the **Workbook** object in Microsoft Excel, and the **Presentation** object in Microsoft PowerPoint returns a **Sync** object.


 **Note**  Beginning with Microsoft Office 2010, this object or member has been deprecated and should not be used.


## Remarks

Use the  **Sync** object to manage the synchronization of the local and server copies of a shared document stored in a SharePoint site. The **Status** property returns important information about the current state of synchronization. Use the **GetUpdate** method to refresh the sync status. Use the **LastSyncTime**, **ErrorType**, and **WorkspaceLastChangedBy** properties to return additional information.

See the  **Status** property for additional information on the differences and conflicts that can exist between the local and server copies of shared documents.

Use the  **PutUpdate** method to save local changes to the server. Close and re-open the document to retrieve the latest version from the server when no local changes have been made. Use the **ResolveConflict** method to resolve differences between the local and the server copies, or the **OpenVersion** method to open a different version alongside the currently open local version of the document.

The  **GetUpdate**, **PutUpdate**, and **ResolveConflict** methods of the **Sync** object do not return status codes because they complete their tasks asynchronously. The **Sync** object provides important status information through a single event, which the developer can access through the following application-specific events:


- in Word, through the  **Sync** event of the **Document** object or the **DocumentSync** event of the **Application** object;
    
- in Excel, through the  **Sync** event of the **Workbook** object or the **WorkbookSync** event of the **Application** object;
    
- in Microsoft PowerPoint, through the  **PresentationSync** event of the **Application** object.
    


The  **Sync** event described above returns an **msoSyncEventType** value.

The  **Sync** object model is available whether sharing and synchronization are enabled or disabled on the active document. The **Sync** property of the **Document**, **Workbook**, and **Presentation** objects does not return **Nothing** when the active document is not shared or synchronization is not enabled. Use the **Status** property to determine whether the document is shared and whether synchronization is enabled.

Not all document synchronization problems raise trappable run-time errors. After using the methods of the  **Sync** object, it's a good idea to check the **Status** property; if the **Status** property is **msoSyncStatusError**, check the **ErrorType** property for additional information on the type of error that has occurred.

In many circumstances, the best way to resolve an error condition is to call the  **GetUpdate** method. For example, if a call to **PutUpdate** results in an error condition, then a call to **GetUpdate** will reset the status to **msoSyncStatusLocalChanges**.


## Example

The following example demonstrates various methods of the  **Sync** object based on the status of the active document.


```
Dim objSync As Office.Sync 
    Dim strStatus As String 
    Set objSync = ActiveDocument.Sync 
    If objSync.Status > msoSyncStatusNoSharedWorkspace Then 
        Select Case objSync.Status 
            Case msoSyncStatusConflict 
                objSync.ResolveConflict msoSyncConflictMerge 
                ActiveDocument.Save 
                objSync.ResolveConflict msoSyncConflictClientWins 
                strStatus = "Conflict resolved by merging changes." 
            Case msoSyncStatusError 
                strStatus = "Last error type: " &amp; objSync.ErrorType 
            Case msoSyncStatusLatest 
                strStatus = "Document copies already in sync." 
            Case msoSyncStatusLocalChanges 
                objSync.PutUpdate 
                strStatus = "Local changes saved to server." 
            Case msoSyncStatusNewerAvailable 
                objSync.GetUpdate 
                strStatus = "Local copy updated from server." 
            Case msoSyncStatusSuspended 
                objSync.Unsuspend 
                strStatus = "Synchronization resumed." 
        End Select 
    Else 
        strStatus = "Not a shared workspace document." 
    End If 
    MsgBox strStatus, vbInformation + vbOKOnly, "Sync Information" 
    Set objSync = Nothing
```


## Methods



|**Name**|
|:-----|
|[GetUpdate](a92c0096-fcf2-2754-31e6-2b20a5841463.md)|
|[OpenVersion](22892531-5e6d-f977-c430-0160cadb4490.md)|
|[PutUpdate](2197cb71-e4d3-e89f-768b-7fd76f92a2d2.md)|
|[ResolveConflict](d127ccab-644c-a2e3-68d1-57138ca200df.md)|
|[Unsuspend](456a5f22-30bf-224d-7e3c-092711188f80.md)|

## Properties



|**Name**|
|:-----|
|[Application](f0bc8dd7-c33b-b34f-6ff6-785c92893203.md)|
|[Creator](c12c2c8f-a94d-e673-d528-0eaf68e75f35.md)|
|[ErrorType](6663e5f6-b90e-29f8-2ff9-f9fb8bda76f0.md)|
|[LastSyncTime](d85af059-a39e-e100-c81a-06265b43cade.md)|
|[Parent](16d56781-dc1e-5d02-b679-11b5ffd70269.md)|
|[Status](fdddff38-268b-835a-7c8d-db76d862e392.md)|
|[WorkspaceLastChangedBy](f2eac8a6-5e94-44a9-3d2f-1ca04cf54361.md)|

## See also


#### Other resources


[Object Model Reference](http://msdn.microsoft.com/library/499c789a-aba2-0fad-649a-0ea964cd3b5e%28Office.15%29.aspx)