
# OutlineCodes Object (Project)

Contains a collection of  **[OutlineCode](8f75bdd3-ed5b-ed0f-9c3c-85af3a21580c.md)** objects.
 


## Remarks

An outline code is a type of local custom field that has a hierarchical text lookup table. Enterprise custom fields of type  **Text** that have hierarchical lookup tables act as outline codes. Use the **[OutlineCodes](400701e8-0114-0819-716f-d79d08a955d5.md)** property to return an **OutlineCodes** collection. Use the **[Add](e33dcb6b-90a3-e52c-099a-f0a901b3f3f7.md)** method to add a local outline code to the **OutlineCodes** collection. To add an enterprise custom field, you must use Project Web App or the Project Server Interface (PSI).
 

 

## Example

 **Using the OutlineCodes Collection Object**
 

 
The following example adds a custom outline code to store the location of resources and configures the outline code such that only values specified in the lookup table can be associated with a resource. 
 

 

 **Note**  The  **OnlyLookUpTableCodes** property can be set only after the lookup table contains entries. If you try to set **OnlyLookUpTableCodes** before creating lookup table entries, the result is run-time error 7, "Out of memory."
 




```
Sub CreateLocationOutlineCode() 

 

 Dim objOutlineCode As OutlineCode 

 

 Set objOutlineCode = ActiveProject.OutlineCodes.Add( _ 

 pjCustomResourceOutlineCode1, "Location") 

 

 DefineLocationCodeMask objOutlineCode.CodeMask 

 EditLocationLookupTable objOutlineCode.LookupTable 

 

 objOutlineCode.OnlyLookUpTableCodes = True 

 

End Sub 

 

 

Sub DefineLocationCodeMask(objCodeMask As CodeMask) 

 objCodeMask.Add _ 

 Sequence:=pjCustomOutlineCodeUppercaseLetters, _ 

 Length:=2, Separator:="." 

 

 objCodeMask.Add _ 

 Sequence:=pjCustomOutlineCodeUppercaseLetters, _ 

 Separator:="." 

 

 objCodeMask.Add _ 

 Sequence:=pjCustomOutlineCodeUppercaseLetters, _ 

 Length:=3, Separator:="." 

End Sub 

 

 

Sub EditLocationLookupTable(objLookupTable As LookupTable) 

 Dim objStateEntry As LookupTableEntry 

 Dim objCountyEntry As LookupTableEntry 

 Dim objCityEntry As LookupTableEntry 

 

 Set objStateEntry = objLookupTable.AddChild("WA") 

 objStateEntry.Description = "Washington" 

 

 Set objCountyEntry = objLookupTable.AddChild("KING", _ 

 objStateEntry.UniqueID) 

 objCountyEntry.Description = "King County" 

 

 Set objCityEntry = objLookupTable.AddChild("SEA", _ 

 objCountyEntry.UniqueID) 

 objCityEntry.Description = "Seattle" 

 

 Set objCityEntry = objLookupTable.AddChild("RED", _ 

 objCountyEntry.UniqueID) 

 objCityEntry.Description = "Redmond" 

 

 Set objCityEntry = objLookupTable.AddChild("KIR", _ 

 objCountyEntry.UniqueID) 

 objCityEntry.Description = "Kirkland" 

End Sub
```


## Methods



|**Name**|
|:-----|
|[Add](e33dcb6b-90a3-e52c-099a-f0a901b3f3f7.md)|

## Properties



|**Name**|
|:-----|
|[Application](88f61596-0f81-7544-5fec-66320b35677c.md)|
|[Count](992586f5-b4f0-3e96-1d6d-5d92bf968ca2.md)|
|[Item](9b5a0d83-02e5-4e85-745e-fca03d6c14a8.md)|
|[Parent](6ec19253-3261-cc81-b01a-9eac71d84925.md)|

## See also


#### Other resources


 
[Project Object Model](http://msdn.microsoft.com/library/900b167b-88ec-ea88-15b7-27bb90c22ac6%28Office.15%29.aspx)