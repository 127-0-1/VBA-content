
# Shape Object (Project)
Represents an object in a Project report, such as a chart, report table, text box, freeform drawing, or picture.
 

## Remarks

The  **Shape** object is a member of the **[Shapes](6e42040c-dd5a-de4c-afa8-f9e33d1e5054.md)** collection, which includes all of the shapes in the report.
 

 

 **Note**  Macro recording for the  **Shape** object is not implemented. That is, when you record a macro in Project and manually add a shape or edit shape elements, the steps for adding and manipulating the shape are not recorded.
 

There are three objects that represent shapes: the  **Shapes** collection, which represents all the shapes on a document; the **ShapeRange** object, which represents a specified subset of the shapes on a document (for example, a **ShapeRange** object could represent shapes one and four on the document, or it could represent all the selected shapes on the document); and the **Shape** object, which represents a single shape on a document. If you want to work with several shapes at the same time or with shapes within the selection, use a **ShapeRange** collection.
 

 
Use  `Shapes(Index)`, where  _Index_ is the shape name or the index number, to return a single **Shape** object.
 

 

## Example

In the following example, the  **TestTextShape** macro creates a textbox shape, adds some text, and changes the shape style, fill, line, shadow, and reflection properties. The **FlipShape** macro flips the shape from top to bottom.
 

 

```
Sub TestTextShape()
    Dim theReport As Report
    Dim textShape As Shape
    Dim reportName As String
    
    reportName = "Simple scalar chart"
    
    Set theReport = ActiveProject.Reports(reportName)
    Set textShape = theReport.Shapes.AddTextbox(msoTextOrientationHorizontal, 30, 30, 300, 100)
    textShape.Name = "TestTextBox"
    
    textShape.TextFrame2.TextRange.Characters.Text = "This is a test. It is only a test. " _
        &amp; "If it had been real information, there would be some real text here."
    textShape.TextFrame2.TextRange.Characters(1, 15).ParagraphFormat.FirstLineIndent = 0
    
    ' Set the font for the first 15 characters to dark blue bold.
    With textShape.TextFrame2.TextRange.Characters(1, 15).Font
        .Fill.Visible = msoTrue
        .Fill.ForeColor.ObjectThemeColor = msoThemeColorAccent5
        .Fill.Transparency = 0
        .Fill.Solid
        .Size = 14
        .Bold = msoTrue
    End With
    
    textShape.ShapeStyle = msoShapeStylePreset42
    
    With textShape.Fill
        .Visible = msoTrue
        .ForeColor.RGB = RGB(255, 255, 0)
        .Transparency = 0
        '.Solid
    End With
   
    With textShape.Line
        .Visible = msoTrue
        .ForeColor.ObjectThemeColor = msoThemeColorText1
        .ForeColor.TintAndShade = 0
        .ForeColor.Brightness = 0
        .Transparency = 0
    End With

    textShape.Shadow.Type = msoShadow22
    textShape.Reflection.Type = msoReflectionType3
End Sub

Sub FlipShape()
    Dim theReport As Report
    Dim theShape As Shape
    Dim reportName As String
    Dim shapeName As String
    
    reportName = "Simple scalar chart"
    shapeName = "TestTextBox"
    
    Set theShape = ActiveProject.Reports(reportName).Shapes(shapeName)

    theShape.Flip msoFlipVertical
    theShape.Select
End Sub
```

Figure 1 shows the result, where the shape is selected to make the ribbon  **FORMAT** tab under **DRAWING TOOLS** available, although the active tab is **DESIGN** under **REPORT TOOLS**. If the shape were not selected,  **DRAWING TOOLS** and the **FORMAT** tab would not be visible.
 

 

**Figure 1. Testing the Shape object model**

 
![Testing the Shape object model](images/pj15_VBA_ShapeObject.gif)
 

 

## Methods



|**Name**|
|:-----|
|[Apply](8d7a29f0-6a69-f643-6726-0c85247fb957.md)|
|[Copy](4dd8511b-b4e9-6af7-bb33-f56874236452.md)|
|[Cut](f7deb163-3f43-206b-a8d4-2b0519d27e0e.md)|
|[Delete](b8e01778-85d4-a8bc-5ebe-1489bb79ce7b.md)|
|[Duplicate](19917b35-589e-1cd3-e9eb-5efa13e02793.md)|
|[Flip](7be8741c-d476-39ab-6aaf-2022417824b9.md)|
|[IncrementLeft](dea230eb-19d9-18e3-b0ec-5b0ec8bbbd20.md)|
|[IncrementRotation](b6a4ee31-b572-ac26-5f31-70cb5f9f390d.md)|
|[IncrementTop](69804932-00eb-b446-aa99-1b83174d004d.md)|
|[PickUp](954390b6-8992-f239-d891-467ec732b0b0.md)|
|[RerouteConnections](97a7a245-641f-3d69-59ff-f3177ac3e84d.md)|
|[ScaleHeight](9dd7a3ee-9149-4aa2-c4ad-5fc0ca45ee21.md)|
|[ScaleWidth](78ab4771-8364-ab1d-5d52-924d7605b833.md)|
|[Select](b96be944-9388-cecc-2c3f-ec25e9f96aec.md)|
|[SetShapesDefaultProperties](13888451-ad56-8900-ce09-4aae1bbd8edf.md)|
|[Ungroup](3b2447ec-2639-e9dd-3c39-172e58f74bbe.md)|
|[ZOrder](e8badff9-fbe5-b6b8-8c33-68cfde3bef38.md)|

## Properties



|**Name**|
|:-----|
|[Adjustments](e82838f6-fc59-b3ae-1e0a-f2acf9e9ea28.md)|
|[AlternativeText](c19d0c9f-87a8-ca8e-75f8-06a974847301.md)|
|[Application](81e03513-4912-ad9c-ef81-c4e860bbefa1.md)|
|[AutoShapeType](697d26ac-a819-8c87-02fc-3006eb8c3c03.md)|
|[BackgroundStyle](358fd8af-4c5a-2fb8-00f0-84c5e0ab832f.md)|
|[BlackWhiteMode](eaa9367b-3a68-6c55-328f-766dd79ff274.md)|
|[Callout](b6ee3e73-6b50-0cbb-9208-006e9a856ed6.md)|
|[Chart](a6119a34-489f-ef5e-3e36-b48cd1134c98.md)|
|[Child](1760dd8a-80d8-fbb5-ad4f-12042ee410be.md)|
|[ConnectionSiteCount](86727754-1d50-f10d-ec8e-91923c09d388.md)|
|[Connector](6085e8e1-1132-ccf7-b37c-3ca86252f656.md)|
|[ConnectorFormat](8bcbe86a-164e-038f-c41a-2d951e549aef.md)|
|[Fill](582892a5-5b3d-69a7-5ca3-df774d54d37a.md)|
|[Glow](4bd06373-53b8-d790-1500-9e77312ab52b.md)|
|[GroupItems](2045400a-37f2-aac7-5fc9-cc4840fbf837.md)|
|[HasChart](4dba3972-c4aa-7f7a-47bb-5e7134965d9f.md)|
|[HasTable](03e919f7-1682-0d1d-7e33-160a3d22db81.md)|
|[Height](1a795abf-2df7-b338-981c-c4c2511e10cd.md)|
|[HorizontalFlip](57862ea8-62b9-0e01-b7c5-017cc7b8e5ef.md)|
|[ID](8b619251-1914-cbf0-6b50-e978f8ffe125.md)|
|[Left](e1128044-f737-e500-f66c-b3ea3f5f1244.md)|
|[Line](76d5bba5-c33f-ea1a-23c3-d7a35fb3f992.md)|
|[LockAspectRatio](b465aad3-d253-d6ce-0f6e-bb0638733647.md)|
|[Name](72a1f223-a47a-7674-0bcc-218f7ff86e63.md)|
|[Nodes](ab95781b-069b-012a-caf5-3c16f54800fd.md)|
|[Parent](2ef9ae28-297c-4ab7-5416-0bb3cd112dd0.md)|
|[ParentGroup](1df4b56c-694d-549a-e64e-29d25270ba2f.md)|
|[Reflection](02aaa497-5941-fdb9-5c34-5a596f11d1af.md)|
|[Rotation](c05b3aaf-7a6b-c900-b648-efe1191ef713.md)|
|[Shadow](52e0453c-d53f-75c2-ef02-6fddfc5f0fab.md)|
|[ShapeStyle](202ac7fe-032b-ec7c-9cd7-6afb042f6f93.md)|
|[SoftEdge](fdb3ce07-488b-163c-63a2-de9ee80f2887.md)|
|[Table](5e1fc97f-8ac8-db26-3a2d-c39261c23588.md)|
|[TextEffect](12fa0951-e3a5-807e-bebb-bff82650d200.md)|
|[TextFrame](73227e78-07d0-874a-85b1-5aac91bff6cf.md)|
|[TextFrame2](9f79d3a3-780c-e0b8-1255-e74e027b9902.md)|
|[ThreeD](2f5ebc06-e59b-bfe1-fb02-0faf2fc1f1df.md)|
|[Title](09cfd863-e682-d64d-bb33-b8dffc30011e.md)|
|[Top](c105b882-49fb-c9ba-25bf-6b62b393ac48.md)|
|[Type](3d64d2c5-5b93-dce7-a8b7-3e789550816a.md)|
|[VerticalFlip](ab7a1b3b-8aaa-ba92-e739-7e7afce6ddd7.md)|
|[Vertices](3db780f4-5407-f83b-f379-e9cdcc6c4946.md)|
|[Visible](646a7ea2-b644-0ee8-3587-50dbaa19ab36.md)|
|[Width](ddf3de99-8667-3d12-10dc-5d355542ae3e.md)|
|[ZOrderPosition](ebbd573a-4cf0-a3af-7dff-de67d321d9d2.md)|

## See also


#### Other resources


 
[Report Object](38ef993e-e5cd-b451-06aa-41eb0e93450e.md)
 
[Chart Object](810d4ec1-69d2-c432-b9da-57042b783b85.md)