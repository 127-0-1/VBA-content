
# OutlineCode Object (Project)


 

Represents a local outline code in Project. The  **OutlineCode** object is a member of the **[OutlineCodes](a2e6d0c7-0741-91c6-61aa-f4bcc299e66f.md)** collection.
 
 **Using the OutlineCode Object**
 
The following example adds a custom outline code to store the location of resources and configures the outline code so that only values specified in the lookup table can be associated with a resource. 
 



```
Sub CreateLocationOutlineCode() 
    Dim objOutlineCode As OutlineCode 
 
    Set objOutlineCode = ActiveProject.OutlineCodes.Add( _
        pjCustomResourceOutlineCode1, "Location") 
 
    objOutlineCode.OnlyLookUpTableCodes = True 
 
    DefineLocationCodeMask objOutlineCode.CodeMask 
    EditLocationLookupTable objOutlineCode.LookupTable 
End Sub 
 
 
Sub DefineLocationCodeMask(objCodeMask As CodeMask) 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=2, Separator:="." 
 
    objCodeMask.Add _ 
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Separator:="." 
 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=3, Separator:="." 
End Sub 
 
 
Sub EditLocationLookupTable(objLookupTable As LookupTable) 
    Dim objStateEntry As LookupTableEntry 
    Dim objCountyEntry As LookupTableEntry 
    Dim objCityEntry As LookupTableEntry 
 
    Set objStateEntry = objLookupTable.AddChild("WA") 
    objStateEntry.Description = "Washington" 
 
    Set objCountyEntry = objLookupTable.AddChild("KING", _
        objStateEntry.UniqueID) 
    objCountyEntry.Description = "King County" 
 
    Set objCityEntry = objLookupTable.AddChild("SEA", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Seattle" 
 
    Set objCityEntry = objLookupTable.AddChild("RED", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Redmond" 
 
    Set objCityEntry = objLookupTable.AddChild("KIR", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Kirkland" 
End Sub
```


## Remarks

An outline code is a type of local custom field that has a hierarchical text lookup table. Enterprise custom fields of type  **Text** that have hierarchical lookup tables act as outline codes. Use the **[OutlineCodes](400701e8-0114-0819-716f-d79d08a955d5.md)** property to return an **OutlineCodes** collection. Use the **[Add](e33dcb6b-90a3-e52c-099a-f0a901b3f3f7.md)** method to add a local outline code to the **OutlineCodes** collection. To add an enterprise custom field, you must use Project Web App or the Project Server Interface (PSI).
 

 

## Methods



|**Name**|
|:-----|
|[Delete](59aa584c-5593-737e-276b-6a61ae2986e2.md)|

## Properties



|**Name**|
|:-----|
|[Application](99a570ec-c14c-3140-3777-22ec0958a6eb.md)|
|[CodeMask](82f6f4df-b69a-62e4-546d-3600a51a28a3.md)|
|[DefaultValue](8b62d945-d98c-923c-2829-e50e51cd45b6.md)|
|[FieldID](eea0a697-08f9-c4f5-358a-6b90bd08271e.md)|
|[Index](9069edd7-fc0a-57a0-4f00-64e295780d3f.md)|
|[LinkedFieldID](310202bc-6db7-11b8-d380-af26ef12ad11.md)|
|[LookupTable](04eedfab-2aff-5836-ec02-5f3a41ab3e77.md)|
|[MatchGeneric](5c724bc3-0d2c-8fdc-1f5e-4b62a7d3f761.md)|
|[Name](b4814e58-2efd-18aa-4018-eb883fc64afa.md)|
|[OnlyCompleteCodes](eb0b8dc2-2cb8-a86b-2711-fa4c6f215971.md)|
|[OnlyLeaves](cc477127-c784-fdea-53b1-7399d18d6b8b.md)|
|[OnlyLookUpTableCodes](172aeac5-98ed-9599-e9d3-cc0afde6b940.md)|
|[Parent](fd9ff0b9-3f51-65c3-88cc-5a0bb9378c20.md)|
|[RequiredCode](39ecd7fd-20d9-c849-c0ef-923dcbe38e4b.md)|
|[SortOrder](5b6014e3-6787-9b32-0446-04415c2f8c87.md)|
